import os

######## Platform configuration ########

platform = str(ARGUMENTS.get('OS', Platform()))
cygwin = platform == "cygwin"
darwin = platform == "darwin"
win32 = cygwin or platform == "windows"

env = Environment()

######## Dependency checks ########

def CheckWx(context, minVersion):
	context.Message('Checking for WxWidgets... ')
	f = os.popen('wx-config --version')
	line = f.readline()
	f.close()
	line = line.rstrip("\r\n")
	context.Result(line)
	return line >= minVersion

conf = Configure(env, custom_tests = {
	'CheckWx' : CheckWx
})
if not conf.CheckWx('2.6.0'):
	print "WxWidgets 2.6.0 or higher is required. You can get it from:"
	print "http://www.wxwidgets.org/"
	Exit(1)
conf.Finish()

######## Setup environment ########
env['CCFLAGS'] = ['-g', '-pipe', '-Wall', '-fvisibility=hidden']
env['LINKFLAGS'] = ['-Wl,--gc-sections']
env['LIBPATH'] = []
env['LIBS'] = []
env['CPPDEFINES'] = []
env['CPPPATH'] = []
if cygwin:
	env['CCFLAGS'] += ['-mno-cygwin']
	env['LINKFLAGS'] += ['-mno-cygwin']

wxenv = env.Copy()
wxenv.ParseConfig('wx-config --cflags base core')
wxenv['CXXFLAGS'] = wxenv['CCFLAGS'] + ['-fvisibility-inlines-hidden']

# ParseConfig can't handle absolute filenames for static libraries
f = os.popen('wx-config --libs base core', 'r')
wxenv.Append(LINKFLAGS = Split(' '.join(f.readlines())))
f.close()

# Linking will mess up if the libraries come before the object files
# (as scons does by default), so we specify a custom linking command.
wxenv['LINKCOM'] = '$LINK $_LIBDIRFLAGS -o $TARGET $SOURCES $_LIBFLAGS $LINKFLAGS'


Export('env wxenv win32 cygwin')
SConscript(['wz/SConscript', 'objdump/SConscript', 'ui/SConscript'])
